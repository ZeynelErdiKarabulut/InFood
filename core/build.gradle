apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: '../deps.gradle'

android {
    compileSdkVersion config.compileSdkVersion
    buildToolsVersion config.buildToolsVersion

    Properties properties = new Properties()
    properties.load(project.rootProject.file("local.properties").newDataInputStream())

    defaultConfig {
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        versionCode config.versionCode
        versionName config.versionName

        buildConfigField("String", "BASE_URL_API", "\"https://api.spoonacular.com/\"")
        buildConfigField("String", "BASE_URL", "\"https://spoonacular.com/\"")
        buildConfigField("String", "SPOONACULAR_API_KEY", properties.getProperty("spoonacular.key"))

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Kotlin
    implementation kotlinDependencies.stdlib
    implementation kotlinDependencies.coroutineCore

    // Support
    implementation supportDependencies.material
    implementation supportDependencies.lifecycleCommonJava

    // Navigation
    api navigationDependencies.fragmentKtx
    api navigationDependencies.uiKtx

    // Dependency Injection
    api injectionDependencies.dagger
    kapt injectionDependencies.daggerCompiler

    // Database
    implementation databaseDependencies.roomKtx
    kapt databaseDependencies.roomCompiler

    // Network
    implementation networkDependencies.retrofit
    implementation networkDependencies.retrofitConverterGson
    implementation networkDependencies.okHttp
    implementation networkDependencies.okHttpLogginInterceptor
    implementation networkDependencies.glide
    kapt networkDependencies.glideCompiler
    debugImplementation networkDependencies.chuckerDebug
    releaseImplementation networkDependencies.chuckerRelease

    // Libraries
    implementation libraries.gson
    implementation libraries.shimmer

    // Unit Test
    testImplementation testDependencies.junit

    // Instrumentation Test
    androidTestImplementation testDependencies.androidxJunit
}